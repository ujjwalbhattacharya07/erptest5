MODULE PaymentMethod;

REQUIRE Pos, IncomingPaymentType, OutgoingPaymentType, RetailSettings;

NAMESPACE Retail;

CLASS PaymentMethod 'Payment method';
name '{Name}' = DATA ISTRING[20] (PaymentMethod);

id '{ID}' = DATA STRING[10] (PaymentMethod); 
paymentMethod = GROUP AGGR PaymentMethod p BY id(p);

cashPaymentMethod = DATA PaymentMethod();
isCash 'Cash' (PaymentMethod pm) = cashPaymentMethod() = pm;  

incomingPaymentType = DATA IncomingPaymentType (PaymentMethod) NONULL;
nameIncomingPaymentType 'Incoming payment type' (PaymentMethod m) = name(incomingPaymentType(m));

outgoingPaymentType = DATA OutgoingPaymentType (PaymentMethod) NONULL;
nameOutgoingPaymentType 'Return payment type' (PaymentMethod m) = name(outgoingPaymentType(m));

FORM paymentMethod 'Payment method'
    OBJECTS pm = PaymentMethod PANEL
    PROPERTIES(pm) name, id, isCash, nameIncomingPaymentType, nameOutgoingPaymentType
    
    EDIT PaymentMethod OBJECT pm
;

DESIGN paymentMethod {
    PROPERTY(nameIncomingPaymentType(pm)) { notNull = TRUE; }
    PROPERTY(nameOutgoingPaymentType(pm)) { notNull = TRUE; }
}

FORM paymentMethods 'Payment methods'
    OBJECTS pm = PaymentMethod
    PROPERTIES(pm) READONLY name, id
    
    LIST PaymentMethod OBJECT pm
;

EXTEND FORM options
    OBJECTS pm = PaymentMethod
    PROPERTIES(pm) READONLY name, id, isCash
    PROPERTIES(pm) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(pm);
    }
}