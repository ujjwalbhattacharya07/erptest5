MODULE PosPaymentMethod;

REQUIRE PaymentMethod, Payment, CashAccount;

NAMESPACE Retail;

disabled 'Disabled' = DATA BOOLEAN (Pos, PaymentMethod);

account = DATA Account (Pos, PaymentMethod);
nameAccount 'Account' (Pos p, PaymentMethod pm) = name(account(p, pm));

CONSTRAINT company(Pos p) AND account(p, PaymentMethod pm) AND NOT holder(account(p, pm)) = company(p)
                CHECKED BY account[Pos,PaymentMethod]
                MESSAGE 'Cash account owner does not match the company in the payment method';

account = DATA CashAccount (Pos);
nameAccount 'Cash account' (Pos p) = name(account(p));

CONSTRAINT company(Pos p) AND account(p) AND NOT holder(account(p)) = company(p)
                CHECKED BY account[Pos]
                MESSAGE 'Central checkout account holder does not match the company in the payment method';

cashAccount (Pos p) = account(p, cashPaymentMethod());
cashBalance 'Cash at the checkout' (Pos p) = balance(cashAccount(p));

EXTEND FORM pos
    PROPERTIES(p) nameAccount

    OBJECTS pm = PaymentMethod
    PROPERTIES(pm) READONLY id, name 
    PROPERTIES(p, pm) disabled, Retail.nameAccount[Pos,PaymentMethod]
;

DESIGN pos {
    tabbedPane {
        MOVE BOX(pm);
    }
}