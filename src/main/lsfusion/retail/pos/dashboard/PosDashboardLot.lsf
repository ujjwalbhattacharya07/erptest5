MODULE PosDashboardLot;

REQUIRE PosDashboard, InvoiceShipmentLot;

NAMESPACE Retail;

EXTEND FORM posDashboard
    PROPERTIES(l) READONLY SHOWIF useLot(item(l)) BACKGROUND RGB(198,230,247) IF NOT quantityLot(l) = quantity(l)
                  lotsQuantity PANEL
;

DESIGN posDashboard {
    info {
        MOVE PROPERTY(lotsQuantity(l)) FIRST { alignment = STRETCH; }
    }
}

processBarCode(Invoice i, InvoiceLine l, STRING s) + {
    LOCAL lot = Lot();
    lot() <- lot(OVERRIDE barCodeToLotId(s), (s AS STRING));

    IF product(lot()) THEN {
        LOCAL line = InvoiceLine();
        
        IF NOT product(lot()) = item(l) THEN {
            IF NOT lastInvoiceLine(i, product(lot())) THEN {
                NEW il = InvoiceLine {
                    invoice(il) <- i;
                    item(il) <- product(lot());
                }
            }                
            line() <- lastInvoiceLine(i, product(lot()));
            SEEK posDashboard.l = line();            
        } ELSE
            line() <- l;                      
            
        quantity(line(), lot()) <- quantity(line(), lot()) (+) 1;
        consumedBarCode() <- TRUE;
    }
}