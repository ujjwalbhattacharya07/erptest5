MODULE ManufacturingOrderBulkCreate;

REQUIRE ManufacturingOrderFilter, InvLedger;

NAMESPACE Manufacturing;

toProduce '{Manufacture}' = DATA LOCAL NUMERIC[16,3] (Item);

dataProduceBom = DATA LOCAL Bom (Item);
produceBom (Item i) = OVERRIDE dataProduceBom(i), defaultBom(i);

nameProduceBom 'Bill of Materials' (Item i) = name(produceBom(i));

changeProduceBom (Item i) {
    DIALOG dialogItemBoms OBJECTS i = i, b = produceBom(i) INPUT NULL DO {
        dataProduceBom(i) <- b;
    }
}

FORM inputProduceQuantity 'Product selection'
    OBJECTS i = Item
    PROPERTIES(i) READONLY name, nameUom, id, idBarCode
    PROPERTIES(i) toProduce
    
    PROPERTIES(i) nameProduceBom BACKGROUND dataProduceBom(i) ON CHANGE changeProduceBom(i)
    
    FILTERS defaultBom(i)
    
    FILTERGROUP toProduce
        FILTER '{Manufacture}' toProduce(i)
;

// products
onHandProducts 'On hand' = onHand(manufacturingOrderProductsLocation(), i);

fillNegative 'Fill negative' () {
    toProduce(Item i) <- -onHandProducts(i) WHERE [FILTER inputProduceQuantity.i](i) AND onHandProducts(i) < 0; 
}

EXTEND FORM inputProduceQuantity
    PROPERTIES() nameManufacturingOrderProductsLocation,
                 fillNegative DRAW i TOOLBAR 
    
    PROPERTIES READONLY onHandProducts(i) BEFORE toProduce(i) SHOWIF manufacturingOrderProductsLocation()
;

// bulk create

bulkCreate 'Create orders' () {
    IF NOT manufacturingOrderType() THEN {
        MESSAGE 'No production order type selected';
        RETURN;
    }
    
    DIALOG inputProduceQuantity DOCKED DO {
        NEWSESSION NESTED (toProduce[Item]){
            FOR toProduce(Item i) DO NEW o = ManufacturingOrder {
                type(o) <- manufacturingOrderType();
                item(o) <- i;
                bom(o) <- produceBom(i);
                materialsLocation(o) <- manufacturingOrderMaterialsLocation();
                productsLocation(o) <- manufacturingOrderProductsLocation();
                fillByQuantity(o, toProduce(i));
            }
            APPLY;
        }
    }
}

EXTEND FORM manufacturingOrders
    PROPERTIES() bulkCreate
;

DESIGN manufacturingOrders {
    actions {
        MOVE PROPERTY(bulkCreate()) { alignment = STRETCH; }
    }
}