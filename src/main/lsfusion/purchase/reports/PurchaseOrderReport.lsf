MODULE PurchaseOrderReport;

REQUIRE PurchaseOrder, ItemAttribute;

NAMESPACE Purchase;

attribute(OrderLine l, ItemAttribute a) = value(attribute(item(l), a));

FORM purchaseOrderReport 'Order report'
    OBJECTS a = ItemAttribute

    OBJECTS o = (o = Order, l = OrderLine) PIVOT
    PROPERTIES(o) READONLY number, dateTime, nameVendor,
                           imagedNameStatus, nameType, vendorReference, 
                           scheduledDateTime, nameLocation,
                           namePaymentTerms, nameRepresentative 
    PROPERTIES(l) READONLY nameItem, idItem, referenceItem 
    PROPERTIES READONLY 'Category 1' = level1(item(l)), 'Category 2' = level2(item(l)), 'Category 3' = level3(item(l)), 'Category 4' = level4(item(l)), 
                        'Canonical group' = canonicalNameCategory(item(l)) 
    PROPERTIES READONLY attribute(l, a) COLUMNS (a) HEADER name(a)
    PROPERTIES(l) READONLY description, nameUom, price, taxes
    PROPERTIES(l) READONLY MEASURE
                           untaxedAmount, taxAmount, amount
    FILTERS order(l) = o,
            accessGranted(location(o)) OR NOT location(o)
;

DESIGN purchaseOrderReport {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
    }
}

@defineDateTimeAggregationForm(purchaseOrderReport, o);
@defineDateFilterForm(purchaseOrderReport, o);

NAVIGATOR {
    reporting {
        NEW purchaseOrderReport;        
    }
}
