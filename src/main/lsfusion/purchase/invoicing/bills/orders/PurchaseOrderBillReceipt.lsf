MODULE PurchaseOrderBillReceipt;

REQUIRE PurchaseOrderBill, PurchaseOrderReceipt, BillReceipt;

NAMESPACE Purchase;

EXTEND CLASS InvoicingPolicy {
    delivered 'Received quantity'
}

toBillShipped (Order o) = 
    GROUP SUM done(ReceiptLine sl) IF order(orderLine(sl)) = o AND active(receipt(sl)) AND NOT billLine(sl);
toBill(o) += toBillShipped(o) > 0;

fill (Bill b, Order o) + {
    location(b) <- location(o);

    IF invoicingPolicy(type(o)) = InvoicingPolicy.delivered THEN {
        FOR order(orderLine(ReceiptLine sl)) = o AND active(receipt(sl)) AND NOT billLine(sl) AND done(sl) > 0 INLINE 
            NEW il = BillLine DO {
            bill(il) <- b;
            orderLine(il) <- orderLine(sl);
    
            item(il) <- product(sl);
            description(il) <- description(orderLine(sl));
            quantity(il) <- done(sl);
            price(il) <- price(orderLine(sl));
            
            billLine(sl) <- il;
            
            fill(il, orderLine(sl));
        }
    }                            
}

fill (ReceiptLine rl, BillLine il) + {
    orderLine(rl) <- orderLine(il);
}