MODULE Selection;

NAMESPACE Utils;

META defineSelection(cls)
    selected 'Sel.' = DATA LOCAL NESTED BOOLEAN (cls);
    background(cls o) += WHEN selected(o) THEN RGB(224,224,255);

    select '+' (cls o) {
        selected(o) <- NOT selected(o);
    } CHANGEKEY 'ADD' HIDE;
END

META defineSelectionForm(form, cls, obj)
    selected###form (cls o) = selected(o) AND [FILTER form.obj](o);
    countSelected###form 'Selected' () = GROUP SUM 1 IF selected###form(cls o);
    
    invertSelection###form '*' () {
        selected(cls o) <- NOT selected(o) WHERE [FILTER form.obj](o);
    } CHANGEKEY 'MULTIPLY' HIDE;
    
    deselectAll###form '-' () {
        selected(cls o) <- NULL WHERE [FILTER form.obj](o);
    } CHANGEKEY 'SUBTRACT' HIDE;
    
    EXTEND FORM form
        PROPERTIES(obj) selected FIRST
    
        PROPERTIES DRAW obj TOOLBAR select(obj), invertSelection###form(), deselectAll###form()
        
        FILTERGROUP selected
            FILTER 'Sel.' selected(obj) 'ctrl O'
    ;
    
    DESIGN form {
        PROPERTY(selected(obj)) { changeOnSingleClick = TRUE; }        
        TOOLBARBOX(obj) {
            NEW selection AFTER TOOLBARLEFT(obj) {
                horizontal = TRUE;
                MOVE PROPERTY(select(obj));
                MOVE PROPERTY(invertSelection###form());
                MOVE PROPERTY(deselectAll###form());
                MOVE FILTERGROUP(selected);
            }
        } 
    }
END

META defineSelectionCopyDateTime (form, cls, dateProp, caption)

    // copy
    FORM dialog###form###dateProp 'Enter date:'##caption
        OBJECTS d = DATETIME PANEL
        PROPERTIES dateTime '' = VALUE(d)
    ;
    
    DESIGN dialog###form###dateProp {
        OBJECTS {
            MOVE PROPERTY(dateTime) { fontSize = 16; fill = 1; }
        }
        PROPERTY(formOk()) { changeKey = 'ENTER'; }
    }
    
    
    copy###form##Selected 'Copy' () {
        DIALOG dialog###form###dateProp OBJECTS d INPUT DO {
            LOCAL NESTED toCopy = BOOLEAN (cls);
            toCopy(cls o) <- selected###form(o);
            NEWSESSION {
                FOR toCopy(cls from) NEW to = cls DO {
                    dateProp(to) <- d;
                    clone(to, from);
                }
                APPLY;
            }
        }
    }
    
    EXTEND FORM form
        PROPERTIES() copy###form##Selected SHOWIF countSelected###form()
    ;
    
    DESIGN form {
        actions {
            MOVE PROPERTY(copy###form##Selected()) { alignment = STRETCH; }
        }
    }

END

META defineSelectionDelete(form, cls)
    deleteSelected###form 'Delete' () {
        LOCAL NESTED toDelete = BOOLEAN (cls);
        toDelete(cls o) <- selected###form(o);
        NEWSESSION {
            DELETE cls o WHERE toDelete(o);
            APPLY;
        }
    } CONFIRM;
    
    EXTEND FORM form
        PROPERTIES() deleteSelected###form SHOWIF countSelected###form()
    ;
    
    DESIGN form {
        actions {
            MOVE PROPERTY(deleteSelected###form()) { alignment = STRETCH; }
        }
    }
END

META defineSelectionAction(form, cls, action, caption, container)
    action###form caption () {
        FOR selected###form(cls o) DO
            action(o);
    }
    
    EXTEND FORM form
        PROPERTIES() action###form SHOWIF countSelected###form()
    ;
    
    DESIGN form {
        container {
            MOVE PROPERTY(action###form()) { alignment = STRETCH; }
        }
    }
END