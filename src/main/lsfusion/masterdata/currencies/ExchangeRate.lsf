MODULE ExchangeRate;

REQUIRE Currency;

NAMESPACE MasterData;

CLASS ExchangeRate 'Exchange rate';
TABLE exchangeRate (ExchangeRate);

name '{Name}' = DATA ISTRING[30] (ExchangeRate) CHARWIDTH 10;

currency = DATA Currency (ExchangeRate) NONULL;
nameCurrency 'Currency' (ExchangeRate er) = name(currency(er));
WHEN LOCAL SET(ExchangeRate er IS ExchangeRate) AND NOT CHANGED(currency(er)) DO currency(er) <- defaultCurrency(); 

defaultExchangeRate = DATA ExchangeRate (Currency);
default 'Default' (ExchangeRate er) = defaultExchangeRate(currency(er)) = er;
changeDefault (ExchangeRate er) {
    INPUT b = BOOLEAN DO
        defaultExchangeRate(currency(er)) <- er IF b;
} 

// lines
CLASS ExchangeRateLine 'Exchange rate line';

exchangeRate 'Exchange rate' = DATA ExchangeRate (ExchangeRateLine) NONULL DELETE;

date 'Date' = DATA DATE (ExchangeRateLine) NONULL;

currency = DATA Currency (ExchangeRateLine) NONULL;
nameCurrency 'Currency' (ExchangeRateLine l) = name(currency(l));

rate 'Rate' = DATA NUMERIC[20,10] (ExchangeRateLine) NONULL;

INDEX exchangeRate(ExchangeRateLine rl), currency(rl), date(rl);

FORM exchangeRate 'Exchange rate'
    OBJECTS o = ExchangeRate PANEL
    PROPERTIES(o) name, nameCurrency, default ON CHANGE changeDefault(o)
    
    OBJECTS l = ExchangeRateLine
    PROPERTIES(l) nameCurrency, date, rate, NEW, DELETE
    FILTERS exchangeRate(l) = o
    
    EDIT ExchangeRate OBJECT o
;

DESIGN exchangeRate {
    PROPERTY(nameCurrency(o)) { notNull = TRUE; }
    PROPERTY(nameCurrency(l)) { notNull = TRUE; }
}

FORM exchangeRates 'Exchange rates'
    OBJECTS o = ExchangeRate
    PROPERTIES(o) READONLY name, nameCurrency, default
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
;

FORM dialogExchangeRates 'Exchange rates'
    OBJECTS o = ExchangeRate
    PROPERTIES(o) READONLY name, nameCurrency
    
    LIST ExchangeRate OBJECT o
;

NAVIGATOR {
    masterData {
        NEW exchangeRates;
    }
}

// calculate rate
rate (ExchangeRate r, Currency from, DATE d) =
    GROUP LAST rate(ExchangeRateLine rl)
          ORDER date(rl), rl
          WHERE exchangeRate(rl) = r AND currency(rl) = from AND date(rl) <= d; 

rate (Currency to, Currency from, DATE d) = rate(defaultExchangeRate(to), from, d);
rate1 (Currency to, Currency from, DATE d) = OVERRIDE rate(to, from, d), 1.0000000000;

rate (Currency from, DATE d) = rate(defaultCurrency(), from, d);
rate1 (Currency from, DATE d) = rate1(defaultCurrency(), from, d); 