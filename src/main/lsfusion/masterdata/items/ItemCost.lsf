MODULE ItemCost;

REQUIRE Item;

NAMESPACE MasterData;

dataCost 'Planned cost' = DATA NUMERIC[10,2] (Item, DATE);

cost 'Planned cost' (Item i, DATE d) = GROUP LAST dataCost(i, DATE di) ORDER di WHERE dataCost(i, di) AND di <= d; 

cost 'Planned cost' (Item i) = cost(i, currentDate()) CHARWIDTH 15 MATERIALIZED;

changeCost (Item i, DATE d) {
    INPUT c = NUMERIC[10,2] DO {
        dataCost(i, d) <- c;  
    } 
}
changeCost (Item i) { changeCost(i, currentDate()); }

// history

FORM addCost 'Enter cost:'
    OBJECTS d 'Start date' = DATE PANEL
    PROPERTIES(d) date 'Enter date:' = VALUE
    
    OBJECTS c 'Cost' = NUMERIC[10,2] PANEL
    PROPERTIES(c) cost 'Enter cost:' = VALUE
;

DESIGN addCost {
    PROPERTY(date) { fontSize = 16; }
    PROPERTY(cost) { fontSize = 16; }
}

addCost 'Add to' (Item i) {
    DIALOG addCost OBJECTS d INPUT, c INPUT DO {
        dataCost(i, d) <- c;
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

FORM itemCostHistory 'Cost change history'
    OBJECTS i = Item PANEL
    
    OBJECTS d = DATE
    PROPERTIES date 'Date' = VALUE(d), dataCost(i, d), addCost(i) DRAW d TOOLBAR
    FILTERS dataCost(i, d)
    ORDERS date DESC
;

showCostHistory 'History' (Item i) {
    SHOW itemCostHistory OBJECTS i = i; 
}

copy (Item n, Item o) + {
    dataCost(n, DATE d) <- dataCost(o, d);
}

EXTEND FORM item
    PROPERTIES(i) cost ON CHANGE changeCost(i), showCostHistory
;

DESIGN item {
    column2 {
        NEW cost {
            horizontal = TRUE;
            MOVE PROPERTY(cost(i));
            MOVE PROPERTY(showCostHistory(i));
        }
    }
}