MODULE ItemUomImport;

REQUIRE ItemUom;

NAMESPACE MasterData;

itemUomExport 'Export unit ratios' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM
            'ItemID' = id(Item i), 'UoMID' = id(Uom u), 'Ratio' = ratio(i, u)
                WHERE ratio(i, u) TO f
        ; 
        open(f());
    }
}

itemUomImport 'Import unit ratios' () {
    LOCAL idItem = STRING[50] (INTEGER);
    LOCAL idUom = STRING[50] (INTEGER);
    LOCAL ratio = NUMERIC[20,10] (INTEGER);
       
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idItem = A, idUom = B, ratio = C; 

            FOR [GROUP SUM 1 BY idItem(INTEGER i)](STRING num) AND NOT item(num) DO {
                MESSAGE 'Missing item in database with code' + ' ' + num + '... Import canceled';
                RETURN;
            }  

            FOR [GROUP SUM 1 BY idUom(INTEGER i)](STRING num) AND NOT item(num) DO {
                MESSAGE 'Unit of measure is missing in the database with code' + ' ' + num + '... Import canceled';
                RETURN;
            }  
            
            FOR id(Item i) = idItem(INTEGER j) AND id(Uom u) = idUom(j) DO
                ratio(i, u) <- ratio(j);
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }    
}

EXTEND FORM migrationData 
    PROPERTIES itemUomExport(), itemUomImport()
;

DESIGN migrationData {
    commons {
        NEW attribute {
            horizontal = TRUE;
            caption = 'Unit ratios';
            MOVE PROPERTY(itemUomExport());
            MOVE PROPERTY(itemUomImport());
        }
    }
}