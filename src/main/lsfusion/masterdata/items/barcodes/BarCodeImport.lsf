MODULE BarCodeImport;

REQUIRE BarCode;

NAMESPACE MasterData;

barCodeExport 'Export barcodes' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF BarCode b AND b IS BarCode) THEN {
            NEW b = BarCode {
                id(b) <- '123';
                item(b) <- GROUP LAST Item i ORDER dataName(i) IF i IS Item;
            }
        }
    
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'ItemID' = idItem(BarCode b), '{ID}' = id(b) TO f;        
        open(f());
    }
}

barCodeImport 'Import barcodes' () {
    LOCAL id = STRING (INTEGER);
    LOCAL idItem = ISTRING[50] (INTEGER);

    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idItem = A, id = B;
            
            FOR [GROUP SUM 1 BY idItem(INTEGER i)](num) AND NOT item[STRING](num) DO {
                MESSAGE 'Missing item in database with code' + ' ' + num + '... Import canceled';
                RETURN;
            }
            
            FOR imported(INTEGER i) AND id(i) DO NEW b = BarCode {
                id(b) <- id(i);
                item(b) <- item(idItem(i));
            }
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}

EXTEND FORM migrationData 
    PROPERTIES barCodeExport(), barCodeImport()
;

DESIGN migrationData {
    commons {
        NEW barCode {
            horizontal = TRUE;
            caption = 'Barcodes';
            MOVE PROPERTY(barCodeImport());
            MOVE PROPERTY(barCodeExport());
        }
    }
}

