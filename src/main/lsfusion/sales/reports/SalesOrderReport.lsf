MODULE SalesOrderReport;

REQUIRE SalesOrder, ItemAttribute;

NAMESPACE Sales;

attribute(OrderLine l, ItemAttribute a) = value(attribute(item(l), a));

FORM salesOrderReport 'Order report'
    OBJECTS a = ItemAttribute

    OBJECTS o = (o = Order, l = OrderLine) PIVOT
    PROPERTIES(o) READONLY number, dateTime, nameCustomer, 
                           nameStatus, nameType, customerReference, 
                           nameDepartment, addressCsvCustomer, deliveryAddress,
                           scheduledDateTime, nameLocation,  
                           validity, namePaymentTerms, nameRepresentative
    PROPERTIES(l) READONLY nameItem
    PROPERTIES READONLY 'Category 1' = level1(item(l)), 'Category 2' = level2(item(l)), 'Category 3' = level3(item(l)), 'Category 4' = level4(item(l)), 
                        'Canonical group' = canonicalNameCategory(item(l)) 
    PROPERTIES READONLY attribute(l, a) COLUMNS (a) HEADER name(a) 
    PROPERTIES(l) READONLY description, quantity, nameUom, price, discount, discountPrice, taxes
    PROPERTIES(l) READONLY MEASURE
                           untaxedAmount, taxAmount, amount
    FILTERS order(l) = o,
            accessGranted(location(o)) OR NOT location(o)
;

DESIGN salesOrderReport {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
    }
}


@defineDateTimeAggregationForm(salesOrderReport, o);
@defineDateFilterForm(salesOrderReport, o);

NAVIGATOR {
    reporting {
        NEW salesOrderReport;        
    }
}
