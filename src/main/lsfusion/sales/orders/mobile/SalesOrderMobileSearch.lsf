MODULE SalesOrderMobileSearch;

REQUIRE SalesOrderMobile, SalesOrderSearch, BarCode;

NAMESPACE Sales;

EXTEND FORM orderMobile
    OBJECTS i = Item PAGESIZE 50
    PROPERTIES(i) READONLY '{First_name}' = name
    PROPERTIES READONLY overListPrice(o, i), available(o, i), 
                        'Packing' = salesPackRatio(i) SHOWIF pack(type(o)) 
    PROPERTIES(o, i) quantity ON CHANGE changeQuantity(o, i)
    PROPERTIES(i) READONLY PANEL pName '' = name, '' = id, '' = idBarCode, '' = nameUom
    ORDERS name(i)
    
    FILTERGROUP available
        FILTER 'Available' available(o, i) > 0 OR NOT i IS Product DEFAULT

    FILTERGROUP order
        FILTER 'Ordered' quantity(o, i)

;

DESIGN orderMobile {
    pane {
        NEW search {
            caption = 'Search';
            NEW filters {
                alignment = STRETCH;
                NEW groupFilter {
                    horizontal = TRUE;
                    alignment = STRETCH;
                    MOVE FILTERGROUPS(i);
                }
            }
            MOVE FILTERS(i);
            MOVE GRID(i) {
                PROPERTY(name(i)) { charWidth = 25; fontSize = 10; }
                PROPERTY(overListPrice(o, i)) { charWidth = 4; }
                PROPERTY(available(o, i)) { charWidth = 4; }
                PROPERTY(salesPackRatio(i)) { charWidth = 4; }
                PROPERTY(quantity(o, i)) { charWidth = 4; }
            }
            MOVE PROPERTY(pName) { alignment = STRETCH; }
            NEW panelRow1 {
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(id(i));
                MOVE PROPERTY(idBarCode(i));
                MOVE PROPERTY(nameUom(i));
            }
            REMOVE TOOLBARSYSTEM(i);
        }
    }
}

@defineNameFilter(orderMobile, i, filters);
@defineCategoryFilter(orderMobile, i, filters);

setQuantity(Item i, INTEGER n) {
    FOR [VIEW orderMobile.o](Order o) DO {
        setQuantity(o, i, n * OVERRIDE salesPackRatio(i), 1);
    }
}

increaseQuantity '+' (Order o, Item i) {
    setQuantity(o, i, quantity(o, i) (+) OVERRIDE salesPackRatio(i), 1);
}

decreaseQuantity '-' (Order o, Item i) {
    setQuantity(o, i, quantity(o, i) (-) OVERRIDE salesPackRatio(i), 1);
}

EXTEND FORM orderMobile
    PROPERTIES(o, i) increaseQuantity, decreaseQuantity
;

DESIGN orderMobile {
    search {
        NEW searchActions {
            horizontal = TRUE;
            alignment = STRETCH;
            NEW searchKeyboard {
                fill = 1;
                lines = 5;
            }
            MOVE PROPERTY(increaseQuantity(o, i)) { fill = 1; focusable = FALSE; }
            MOVE PROPERTY(decreaseQuantity(o, i)) { fill = 1; focusable = FALSE; }
        }
    }
}

@defineKeyboard(orderMobile, Item, i, searchKeyboard, 16, setQuantity);
