MODULE BillReport;

REQUIRE CreditNote, BillCurrency, ItemAttribute;

NAMESPACE Invoicing;

attribute(BillLine l, ItemAttribute a) = value(attribute(item(l), a));

FORM billReport 'Bills report'
    OBJECTS a = ItemAttribute

    OBJECTS i = (i = Bill, l = BillLine) PIVOT
    PROPERTIES(i) READONLY number, dateTime, nameVendor,
                           imagedNameStatus, nameType, 
                           nameVendorAccount, nameCompany, nameCompanyAccount, nameCurrency, 
                           namePaymentTerms, dueDateTime, note
    PROPERTIES(l) READONLY nameItem, idItem, referenceItem
    PROPERTIES READONLY 'Category 1' = level1(item(l)), 'Category 2' = level2(item(l)), 'Category 3' = level3(item(l)), 'Category 4' = level4(item(l)), 
                        'Canonical group' = canonicalNameCategory(item(l)) 
    PROPERTIES READONLY attribute(l, a) COLUMNS (a) HEADER name(a)
    PROPERTIES(l) READONLY quantity, price, taxes 
    PROPERTIES(l) READONLY MEASURE
                           untaxedAmount, taxAmount, amount, currencyAmount
    FILTERS bill(l) = i
;

DESIGN billReport {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
    }
}

@defineDateTimeAggregationForm(billReport, i);
@defineDateFilterForm(billReport, i);

NAVIGATOR {
    invoicing {
        reporting {
            NEW billReport;        
        }
    }
}