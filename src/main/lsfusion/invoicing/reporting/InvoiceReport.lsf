MODULE InvoiceReport;

REQUIRE CreditNote, InvoiceCurrency, ItemAttribute;

NAMESPACE Invoicing;

attribute(InvoiceLine l, ItemAttribute a) = value(attribute(item(l), a));

FORM invoiceReport 'Invoices report'
    OBJECTS a = ItemAttribute

    OBJECTS i = (i = Invoice, l = InvoiceLine) PIVOT
    PROPERTIES(i) READONLY number, dateTime, nameCustomer, 
                           imagedNameStatus, nameType, 
                           nameDepartment, nameCustomerAccount, nameCompany, nameCompanyAccount, nameCurrency, 
                           namePaymentTerms, dueDateTime, note
    PROPERTIES(l) READONLY nameItem, idItem, referenceItem
    PROPERTIES READONLY 'Category 1' = level1(item(l)), 'Category 2' = level2(item(l)), 'Category 3' = level3(item(l)), 'Category 4' = level4(item(l)), 
                        'Canonical group' = canonicalNameCategory(item(l)) 
    PROPERTIES READONLY attribute(l, a) COLUMNS (a) HEADER name(a)
    PROPERTIES(l) READONLY quantity, price, taxes
    PROPERTIES(l) READONLY MEASURE
                           untaxedAmount, taxAmount, amount, currencyAmount
    FILTERS invoice(l) = i
;

DESIGN invoiceReport {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
    }
}

@defineDateTimeAggregationForm(invoiceReport, i);
@defineDateFilterForm(invoiceReport, i);

NAVIGATOR {
    invoicing {
        reporting {
            NEW invoiceReport;        
        }
    }
}