MODULE BankImport;

REQUIRE Bank;

NAMESPACE Invoicing;

bankExport 'Bank export' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM '{ID}' = id(Bank b), '{Name}' = name(b),
                                'CountryID' = id(country(b)), 'City' = city(b),
                                'Address' = address(b), 'Phone' = phone(b) TO f;   
        open(f());
    }
}

bankImport 'Bank import' () {
    LOCAL id = STRING[11] (INTEGER);
    LOCAL name = ISTRING[100] (INTEGER);
    LOCAL idCountry = STRING[3] (INTEGER);
    LOCAL city = ISTRING[30] (INTEGER);
    LOCAL address = ISTRING[100] (INTEGER);
    LOCAL phone = ISTRING[100] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id = A, name = B, idCountry = C,
                                        city = D, address = E, phone = F;
            
            FOR imported(INTEGER i) DO NEW b = Bank{
                id(b) <- id(i);
                name(b) <- name(i);
                country(b) <- country(idCountry(i));
                city(b) <- city(i);
                address(b) <- address(i);
                phone(b) <- phone(i);
            }
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}    

EXTEND FORM migrationData 
    PROPERTIES bankExport(), bankImport()
;

DESIGN migrationData {
    commons {
        NEW bank {
            horizontal = TRUE;
            caption = 'Banks';
            MOVE PROPERTY(bankImport());
            MOVE PROPERTY(bankExport());
        }
    }
}