MODULE TaxItemImport;

REQUIRE TaxItem;

NAMESPACE Invoicing;

taxItemImportSales 'Import sales taxes' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL group = STRING[20] (INTEGER);
    LOCAL value = NUMERIC[5,2] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS FROM f TO id = A, group = B, value = C;
            
            FOR imported(INTEGER i) AND id(Item item) = id(i) DO {
                salesIn(item, tax(taxGroup(group(i)), value(i))) <- TRUE;
            } 
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}

taxItemImportPurchase 'Import purchase taxes' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL group = STRING[20] (INTEGER);
    LOCAL value = NUMERIC[5,2] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS FROM f TO id = A, group = B, value = C;
            
            FOR imported(INTEGER i) AND id(Item item) = id(i) DO {
                purchaseIn(item, tax(taxGroup(group(i)), value(i))) <- TRUE;
            } 
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}


taxItemExportSales 'Export sales taxes' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM '{ID}' = id(Item i), 'TaxGroupID' = id(taxGroup(Tax t)), 'Value' = value(t) WHERE salesIn(i, t) TO f;   
        open(f());
    }
}

taxItemExportPurchase 'Export purchase taxes' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM '{ID}' = id(Item i), 'TaxGroupID' = id(taxGroup(Tax t)), 'Value' = value(t) WHERE purchaseIn(i, t) TO f;   
        open(f());
    }
}

EXTEND FORM migrationData 
    PROPERTIES taxItemImportSales(), taxItemExportSales(), taxItemImportPurchase(), taxItemExportPurchase()
;

DESIGN migrationData {
    commons {
        NEW tax {
            horizontal = TRUE;
            caption = 'Taxes';
            NEW importTax {
                MOVE PROPERTY(taxItemImportSales());
                MOVE PROPERTY(taxItemImportPurchase());
            }
            NEW exportTax {
                MOVE PROPERTY(taxItemExportSales());
                MOVE PROPERTY(taxItemExportPurchase());
            }
        }
    }
}