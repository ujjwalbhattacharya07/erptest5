MODULE CashAccountImport;

REQUIRE CashAccount, Partner;

NAMESPACE Invoicing;

cashAccountExport 'Export cash registers' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'HolderID' = id(holder(CashAccount c)), 'Note' = note(c),
                                '{Name}' = name(c) WHERE c IS CashAccount TO f;
        open(f());
    }
}

cashAccountImport 'Import cash registers' () {
    LOCAL idPartner = STRING[50] (INTEGER);
    LOCAL note = ISTRING[100] (INTEGER);
    LOCAL name = ISTRING[100] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idPartner = A, note = B, name = C;

            FOR [GROUP SUM 1 BY idPartner(INTEGER i)](num) AND NOT partner[STRING](num) DO {
                MESSAGE 'There is no partner in the database with the code' + ' ' + num + '... Import canceled';
                RETURN;
            } 
            
            FOR imported(INTEGER i)  AND
                partner(idPartner(i))
                DO NEW c = CashAccount {
                    holder(c) <- partner(idPartner(i));
                    note(c) <- note(i);
                    name(c) <- name(i); 
            }
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}    

EXTEND FORM migrationData 
    PROPERTIES cashAccountExport(), cashAccountImport()
;

DESIGN migrationData {
    commons {
        NEW cashAccount {
            horizontal = TRUE;
            caption = 'Cash accounts';
            MOVE PROPERTY(cashAccountImport());
            MOVE PROPERTY(cashAccountExport());
        }
    }
}
