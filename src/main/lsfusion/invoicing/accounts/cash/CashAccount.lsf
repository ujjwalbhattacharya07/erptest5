MODULE CashAccount;

REQUIRE Account;

NAMESPACE Invoicing;

CLASS CashAccount 'Cash account' : Account;

name '{Name}' = DATA ISTRING[100] (CashAccount);
name (CashAccount a) += name(a); 

EXTEND CLASS AccountType {
    cash 'Cash account'
}

type(Account a) += WHEN a IS CashAccount THEN AccountType.cash;

EXTEND FORM account
    PROPERTIES(a) name[CashAccount]
;

DESIGN account {
    OBJECTS {
        MOVE PROPERTY(name(a)) AFTER params;
    }
}

newCashAccount 'Cash account' () {
    NEWSESSION {
        NEW c = CashAccount { 
            DIALOG account OBJECTS a = c DOCKED DO seek(c);  
        }
    }
} IMAGE 'add.png';

newCashAccount 'Cash account' (Partner p) {
    NESTEDSESSION {
        NEW c = CashAccount {
            holder(c) <- p;
            DIALOG account OBJECTS a = c DOCKED DO seek(c);  
        }
    }
} IMAGE 'add.png';

EXTEND FORM accounts
    PROPERTIES newCashAccount() DRAW a TOOLBAR
    
    LIST CashAccount OBJECT a
;

EXTEND FORM partner
    PROPERTIES newCashAccount(p) DRAW a TOOLBAR
;

FORM cashAccounts 'Cash accounts'
    OBJECTS a = CashAccount
    PROPERTIES(a) READONLY name, nameAccountHolder, note
    
    LIST CashAccount OBJECT a
;

countCashAccount (Partner p) = GROUP SUM 1 IF CashAccount a IS CashAccount AND holder(a) = p; 
calcDefaultCashAccount (Partner p) = GROUP LAST CashAccount a ORDER a WHERE holder(a) = p IF countCashAccount(p) = 1;
defaultCashAccount (Partner p) = OVERRIDE defaultAccount(p) AS CashAccount, calcDefaultCashAccount(p);
