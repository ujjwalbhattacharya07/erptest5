MODULE ReceiptImport;

REQUIRE ReceiptDone, InvLedger, ReceiptCost, ItemPurchase;

NAMESPACE Inventory;

receiptExport 'Export on hand' () {
    LOCAL f = EXCELFILE ();
    
    IF GROUP SUM 1 IF onHand(Location l, Product p) THEN
        EXPORT XLSX HEADER FROM 'LocationID' = id(Location l), 'ProductID' = id(Product p), 
                                'Quantity' = onHand(l, p), 'Cost' = costAmount(l,p) WHERE onHand(l, p) TO f;   
    ELSE 
        EXPORT XLSX HEADER FROM 'LocationID' = '111', 'ProductID' = '222', 
                                'Quantity' = '1', 'Cost' = 1.43 TO f;
    open(f());
}

receiptImport 'Import on hand' () {
    LOCAL idLocation = STRING[50] (INTEGER);
    LOCAL idProduct = ISTRING[50] (INTEGER);
    LOCAL quantity = NUMERIC[16,3] (INTEGER);
    LOCAL cost = NUMERIC[14,2] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idLocation = A, idProduct = B, quantity = C, cost = D;
            
            FOR [GROUP SUM 1 BY idLocation(INTEGER i)](num) AND NOT location[STRING](num) DO {
                MESSAGE 'Missing_storage_location_in_database_with_code' + ' ' + num + '... Import canceled';
                RETURN;
            }
            
            FOR [GROUP SUM 1 BY idProduct(INTEGER i)](num) AND NOT item[STRING](num) DO {
                MESSAGE 'There is no product in the database with a code' + ' ' + num + '... Import canceled';
                RETURN;
            }
            
            IF NOT (GROUP SUM 1 IF ReceiptType t AND t IS ReceiptType) THEN {
                MESSAGE 'There are no receipt types in the database. Import canceled';
                RETURN;
            }
            
            FOR [GROUP SUM 1 BY idLocation(INTEGER i)](num) AND NOT receipt('INIT_' + num) DO NEW nr = Receipt {
                number(nr) <- 'INIT_' + num;
            }
            
            FOR INTEGER i = [GROUP MIN INTEGER ii BY idLocation(ii)](num) AND number(Receipt r) = 'INIT_' + num DO {
                scheduledDateTime(r) <- currentDateTime();
                location(r) <- location(idLocation(i));
                type(r) <- receiptType('initial');
                done(r) <- TRUE;
            }
            
            FOR idProduct(INTEGER i) DO NEW l = ReceiptLine {
                receipt(l) <- receipt('INIT_' + idLocation(i));
                product(l) <- item(idProduct(i));
                initialDemand(l) <- quantity(i);
                done(l) <- quantity(i);
                dataCost(l) <- cost(i);
            }
                        
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}

EXTEND FORM migrationData 
    PROPERTIES receiptImport(), receiptExport()
;

DESIGN migrationData {
    commons {
        NEW receipt {
            horizontal = TRUE;
            caption = 'On hand';
            MOVE PROPERTY(receiptImport());
            MOVE PROPERTY(receiptExport());
        }
    }
}


