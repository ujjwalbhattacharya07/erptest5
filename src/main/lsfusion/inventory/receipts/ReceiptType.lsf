MODULE ReceiptType;

REQUIRE Numerator, Location;

NAMESPACE Inventory;

CLASS ReceiptType 'Receipt type';
name '{Name}' = DATA ISTRING[50] (ReceiptType) NONULL CHARWIDTH 15;

id '{ID}' = DATA STRING[20] (ReceiptType);
receiptType = GROUP AGGR ReceiptType t BY id(t);

numerator = DATA Numerator (ReceiptType);
nameNumerator 'Numerator' (ReceiptType o) = name(numerator(o));

defaultLocation = DATA Location (ReceiptType);
nameDefaultLocation 'Location' (ReceiptType o) = name(defaultLocation(o));

maxQuantity 'Maximum quantity' = DATA NUMERIC[16,3] (ReceiptType);

FORM receiptType 'Receipt type'
    OBJECTS o = ReceiptType PANEL 
    PROPERTIES(o) name, id, nameNumerator, nameDefaultLocation, maxQuantity
     
    EDIT ReceiptType OBJECT o;
;

DESIGN receiptType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
        }
    }
}

FORM receiptTypes 'Types of receipts'
    OBJECTS o = ReceiptType
    PROPERTIES(o) READONLY name, id
    LIST ReceiptType OBJECT o;
;

EXTEND FORM options
    OBJECTS rt = ReceiptType
    PROPERTIES(rt) READONLY name, id
    PROPERTIES(rt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(rt);
    }
}

// default type
countReceiptTypes 'Number of types' () = GROUP SUM 1 IF r IS ReceiptType; 
firstReceiptType = GROUP LAST ReceiptType r ORDER DESC r WHERE r IS ReceiptType;

defaultReceiptType = firstReceiptType() IF countReceiptTypes() = 1;