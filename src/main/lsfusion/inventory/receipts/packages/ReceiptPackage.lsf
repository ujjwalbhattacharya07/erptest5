MODULE ReceiptPackage;

REQUIRE Receipt, Package;

NAMESPACE Inventory;

in 'Incl.' = DATA BOOLEAN (Receipt, Package);

countPackages 'Packages' (Receipt r) = GROUP SUM 1 IF in(r, Package p) MATERIALIZED;

packageReference (Receipt r, STRING[100] reference) = GROUP MIN Package p IF in(r, p) AND reference(p) = reference;

addPackage 'Add to' (Receipt r) {
    DIALOG packages OBJECTS p INPUT DO in(r, p) <- TRUE;
}
remove 'Remove' (Receipt r, Package p) {
    in(r, p) <- NULL;
} IMAGE 'delete.png';

EXTEND FORM receipt
    OBJECTS pc = Package
    PROPERTIES(pc) READONLY id, reference, quantity
    PROPERTIES(pc) EDIT GRID
    PROPERTIES(r, pc) remove GRID
    PROPERTIES(r) addPackage DRAW pc TOOLBAR
    FILTERS in(r, pc)
    
    OBJECTS pcl = PackageLine
    PROPERTIES(pcl) index READONLY, 
                    nameProduct, nameUom, idBarCodeProduct, idProduct, referenceProduct, 
                    quantity
    FILTERS package(pcl) = pc
;

DESIGN receipt {
    details {
        NEW packages {
            caption = badged('Packages', countPackages(r));
            horizontal = TRUE;
            MOVE BOX(pc);
            MOVE BOX(pcl) { fill = 3; }
        }
    }
}