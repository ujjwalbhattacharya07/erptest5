MODULE LegalEntityImportRu;

REQUIRE LegalEntityRu, PartnerPurchase, PartnerSales;

NAMESPACE MasterData;

legalEntityExportRu 'Экспорт юридических данных' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF LegalEntity l IS LegalEntity) THEN {
            NEW l = LegalEntity {
                id(l) <- '123';
                name(l) <- 'Legal entity';
            }
        }
            
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Код' = id(LegalEntity l), 'ИНН' = inn(l), 'КПП' = kpp(l), 'ОКПО' = okpo(l), 'ОГРН' = ogrn(l) WHERE l IS LegalEntity TO f;   
        open(f());
    }
}

legalEntityImportRu 'Импорт юридических данных' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL inn = ISTRING[12] (INTEGER);
    LOCAL kpp = ISTRING[9] (INTEGER);
    LOCAL okpo = ISTRING[8] (INTEGER);
    LOCAL ogrn = ISTRING[15] (INTEGER);
           
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id, inn, kpp, okpo, ogrn;
            FOR id(LegalEntity l) = id(INTEGER j) DO {
                inn(l) <- inn(j);
                kpp(l) <- kpp(j);
                okpo(l) <- okpo(j);
                ogrn(l) <- ogrn(j);
            }            
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }    
}

EXTEND FORM migrationData 
    PROPERTIES legalEntityImportRu(), legalEntityExportRu()
;

DESIGN migrationData {
    commons {
        NEW legalEntityRu {
            horizontal = TRUE;
            caption = 'ИНН и ОКПО организаций';
            MOVE PROPERTY(legalEntityImportRu());
            MOVE PROPERTY(legalEntityExportRu());
        }
    }
}
